prototype(Community.News:News) < prototype(Neos.NodeTypes:Page) {
    body {
        templatePath = 'resource://Community.News/Private/Templates/NodeTypes/News.html'

        title = ${q(node).property('title')}
        publishDate = ${q(node).property('publishDate')}
        teaser = ${q(node).property('teaser')}
        author = ${q(node).property('author')}

        main = Neos.Neos:PrimaryContent {
            nodePath = 'main'
        }

        categories = Neos.Fusion:Collection {
            collection = ${q(node).property('categories')}
            itemName = 'node'
            itemRenderer = Community.News:Category.Short
        }

        # Fetch and render all related news
        relatedNews = Community.News:AbstractList {
            @context.listTitle = 'Related news'
            prototype(Flowpack.Listable:PaginatedCollection) {
                @context.paginationEnabled = false
                collection = ${q(node).property('related')}
            }
            @if.hasRelatedNews = ${q(node).property('related')}
        }
    }
}
